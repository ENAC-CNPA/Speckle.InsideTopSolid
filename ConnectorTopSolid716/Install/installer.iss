; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;To be manually set
#define AppPublisher "EPFL"
#define WebSite "https://speckle.systems/"
#define Project "Speckle"
#define AppName "ConnectorTopSolid"
#define Guid "{79A9EAFE-A578-4C98-8871-0D743CF83FDA}"
#define SrcFolder "C:\Sources\Speckle.TopSolid\setup\bin"
#define IconPath "C:\Sources\Speckle.TopSolid\ConnectorTopSolid716\UI\ContextMenu.Item.ico"
#define ServerRes "res://Services"
#define UpdateType "Services"

;Automatically computed
#define AppVersion GetStringFileInfo(SrcFolder+"\"+"EPFL.SpeckleTopSolid.AddIn.dll","ProductVersion")
#define TopSolidVersion RemoveFileExt(RemoveFileExt(AppVersion))
#define AppVerName AppName+" v"+AppVersion
#define AppVerName_ StringChange(AppVerName," ", "_");

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{#Guid}
AppName={#Project} {#AppVerName}
AppVersion={#AppVersion}
AppVerName={#Project} {#AppVerName}
AppPublisher={#AppPublisher}
AppPublisherURL={#WebSite}
AppSupportURL={#WebSite}
AppUpdatesURL={#WebSite}
CreateAppDir=no
OutputDir=Binaries
OutputBaseFilename={#Project}_{#AppVerName_}_Setup
SetupIconFile={#IconPath}
UninstallDisplayIcon={code:GetTopDir}\bin\topsolid.exe
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"

[Files]
Source: "{#SrcFolder}\EPFL.SpeckleTopSolid.AddIn.dll"; DestDir: "{code:GetTopDir}\bin"; Flags: ignoreversion; Check: IsTopSolidDesignInstalled; 
Source: "{#SrcFolder}\EPFL.SpeckleTopSolid.UI.dll"; DestDir: "{code:GetTopDir}\bin"; Flags: ignoreversion; Check: IsTopSolidDesignInstalled; 

[Registry]

;Application regs
Root: HKLM64; Subkey: "SOFTWARE\{#AppPublisher}\{#AppName}"; Flags: uninsdeletekeyifempty;
Root: HKLM64; Subkey: "SOFTWARE\{#AppPublisher}\{#AppName}\{#TopSolidVersion}"; ValueType: string; ValueName: "INSTALLDIR"; ValueData: "{code:GetTopDir}";
Root: HKLM64; Subkey: "SOFTWARE\{#AppPublisher}\{#AppName}\{#TopSolidVersion}"; ValueType: string; ValueName: "Version"; ValueData: "{#AppVersion}";

;Custom Updates regs
Root: HKLM64; Subkey: "SOFTWARE\{#AppPublisher}\TopSolid'Update\CustomUpdates"; Flags: uninsdeletekeyifempty;
Root: HKLM64; Subkey: "SOFTWARE\{#AppPublisher}\TopSolid'Update\CustomUpdates\{#ServerRes}"; Flags: uninsdeletekeyifempty;
Root: HKLM64; Subkey: "SOFTWARE\{#AppPublisher}\TopSolid'Update\CustomUpdates\{#ServerRes}\{#TopSolidVersion}\x64\{#AppName}"; ValueType: string; ValueName: "CommandLineArguments"; ValueData: "/VERYSILENT"
Root: HKLM64; Subkey: "SOFTWARE\{#AppPublisher}\TopSolid'Update\CustomUpdates\{#ServerRes}\{#TopSolidVersion}\x64\{#AppName}"; ValueType: string; ValueName: "Executable"; ValueData: "TopSolid.exe"
Root: HKLM64; Subkey: "SOFTWARE\{#AppPublisher}\TopSolid'Update\CustomUpdates\{#ServerRes}\{#TopSolidVersion}\x64\{#AppName}"; ValueType: string; ValueName: "UpdateType"; ValueData: "{#UpdateType}"

[Code]
var
  TopDir: String;
  UpdateDir : String;
  TopSolidDesignInstalled : Boolean;

function FuncTopDir(dummy: String): String;
begin
  RegQueryStringValue(HKLM64, 'Software\{#AppPublisher}\TopSolid''Design\7.16', 'INSTALLDIR', TopDir);
  RegQueryStringValue(HKLM64, 'Software\{#AppPublisher}\TopSolid''Update', 'INSTALLDIR', UpdateDir);
  TopSolidDesignInstalled := DirExists(TopDir);
  Result := '';
end;

function GetTopDir(dummy: String): String;
begin
  Result := TopDir;
end;

function GetUpdateDir(dummy: String): String;
begin
  Result := UpdateDir;
end;

function IsTopSolidDesignInstalled(): Boolean;
begin
  Result := TopSolidDesignInstalled;
end;

// EVENT : Setup initialization.
function InitializeSetup(): Boolean;
begin
    // Check TopSolid main path
    FuncTopDir('');
    if DirExists(UpdateDir) or DirExists(TopDir) = True then begin
      Result := True;
    end else begin
      MsgBox('TopSolid directory is not found', mbInformation, MB_OK);
      Result := False;
    end;
end;

[Setup]
;WizardImageFile=Setup.bmp
;WizardSmallImageFile=Program.bmp